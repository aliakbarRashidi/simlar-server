/*
 * Copyright (C) 2015 The Simlar Authors.
 *
 * This file is part of Simlar. (https://www.simlar.org)
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 *
 */

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "war"
    id "pmd"
    id "com.github.spotbugs" version "2.0.0"
    id "org.springframework.boot" version "2.1.5.RELEASE"
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "org.owasp.dependencycheck" version "5.0.0-M3.1"
}

ext {
    lombokVersion = "1.18.8"
    spotbugsVersion = "3.1.12"
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")

    compileOnly("com.github.spotbugs:spotbugs-annotations:$spotbugsVersion")
    testCompileOnly("com.github.spotbugs:spotbugs-annotations:$spotbugsVersion")
    spotbugs("com.github.spotbugs:spotbugs:$spotbugsVersion")
    spotbugsPlugins("com.mebigfatguy.fb-contrib:fb-contrib:7.4.5")
    spotbugsPlugins("com.h3xstream.findsecbugs:findsecbugs-plugin:1.9.0")

    implementation("org.springframework.boot:spring-boot-starter-web-services")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.apache.commons:commons-lang3:3.9")
    implementation("org.apache.commons:commons-text:1.6")
    implementation("commons-codec:commons-codec:1.12")
    implementation("com.googlecode.libphonenumber:libphonenumber:8.10.13")
    implementation("org.glassfish.jaxb:jaxb-runtime:2.3.2")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.9.9") // TODO: remove once spring updates its bom

    runtime("org.mariadb.jdbc:mariadb-java-client:2.4.1")
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

    testImplementation("com.h2database:h2:1.4.199")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.mockito:mockito-core:2.28.2")
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    jcenter()
}

pmd {
    toolVersion = "6.15.0"
    ruleSetFiles = files("pmd-ruleset.xml")
    ruleSets = [ ]
}

spotbugs {
    effort = "max"
    reportLevel = "low"
    excludeFilter = file("$rootProject.projectDir/ides/spotbugs/excludeFilter.xml")
}

//noinspection GroovyAssignabilityCheck,UnnecessaryQualifiedReference
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencyCheck {
    suppressionFile = "owasp-dependency-check-suppression.xml"
    failBuildOnCVSS = 0
}

//noinspection GroovyAssignabilityCheck
task dependencyChecks {
}
dependencyChecks.dependsOn dependencyCheckAnalyze
dependencyChecks.dependsOn dependencyUpdates

processResources {
    filesMatching("**/application.properties") {
        expand(project.properties)
    }
}

//noinspection GroovyMissingReturnStatement
test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

version = getGitVersion()

war {
    enabled = true
    baseName = "simlar-server"
    archiveName = "${baseName}##${version}.war"
    rootSpec.exclude("application-default.properties")
    manifest {
        attributes("Implementation-Version": version)
    }
}

bootWar {
    classifier = 'boot'
    rootSpec.exclude("application-default.properties")
    manifest {
        attributes("Implementation-Version": version)
    }
}

idea {
    module {
        /// IntelliJ inspection excludes
        excludeDirs += file("ides")
        excludeDirs += file("examples")
        excludeDirs += file("pmd-ruleset.xml")
        excludeDirs += file("owasp-dependency-check-suppression.xml")
    }
}
