/*
 * Copyright (C) 2015 The Simlar Authors.
 *
 * This file is part of Simlar. (https://www.simlar.org)
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 *
 */

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "war"
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.owasp.dependencycheck"

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

version = getGitVersion()

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.2.RELEASE")
        classpath("com.github.ben-manes:gradle-versions-plugin:0.17.0")
        classpath("org.owasp:dependency-check-gradle:3.1.2")
    }
}

bootWar {
    baseName = "simlar-server"
    archiveName = "${baseName}##${version}.war"
    rootSpec.exclude("application-default.properties")
    manifest {
        attributes("Implementation-Version": version)
    }
}

repositories {
    mavenCentral()
}

ext {
    lombokVersion = "1.16.20"
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web-services")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("com.h2database:h2:1.4.197")
    implementation("com.google.code.findbugs:annotations:3.0.1")
    implementation("org.apache.commons:commons-lang3:3.7")
    implementation("org.apache.commons:commons-text:1.3")
    implementation("commons-codec:commons-codec:1.11")
    implementation("com.googlecode.libphonenumber:libphonenumber:8.9.5")
    implementation("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.mockito:mockito-core:2.18.3")
    runtime("org.mariadb.jdbc:mariadb-java-client:2.2.3")

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat") // disable this to run in IntelliJ CE

    findbugs("com.google.code.findbugs:findbugs:3.0.1")
    /// IntelliJ plugin uses this version
    /// see: https://github.com/andrepdo/findbugs-idea/tree/master/src/src/impl/org/twodividedbyzero/idea/findbugs/plugins
    findbugsPlugins("com.mebigfatguy.fb-contrib:fb-contrib:6.8.2")
}

//noinspection GroovyMissingReturnStatement
test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

pmd {
    /// IntelliJ plugin uses this version
    /// see: https://github.com/amitdev/PMD-Intellij/blob/master/PMD-Intellij.iml
    toolVersion = "6.0.1"
    ruleSetFiles = files("pmd-ruleset.xml")
}

findbugs {
    effort = "max"
    reportLevel = "low"
    excludeFilter = file("$rootProject.projectDir/ides/findbugs/excludeFilter.xml")
}

//noinspection GroovyAssignabilityCheck
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencyCheck {
    suppressionFile = "owasp-dependency-check-suppression.xml"
    failBuildOnCVSS = 0
}
check.dependsOn dependencyCheckAnalyze

task wrapper(type: Wrapper) {
    gradleVersion = "4.7"
}

processResources {
    filesMatching("**/application.properties") {
        expand(project.properties)
    }
}

idea {
    module {
        excludeDirs += file("ides")
        excludeDirs += file("examples")
    }
}
